apply plugin: 'com.android.application'
def cmd = 'git rev-list HEAD --count'
def gitVersion = cmd.execute().text.trim().toInteger()
android {
    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.hxm.rxjava2retrofit2mvp"
        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion
        versionCode gitVersion
        versionName "V1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        //jks密码文件路径，根据实际路径修改,properties见项目里的release.properties
        def proPath = 'D:\\Android\\keystore\\lovegx\\release.properties'
        //jks文件路径，根据实际路径修改
        def jksPath = 'D:\\Android\\keystore\\lovegx\\xxxx.jks'
        Properties properties = new Properties()
        File propFile = project.file(proPath)
        if (propFile.exists()) {
            properties.load(propFile.newDataInputStream())
        }
        release {
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
            storeFile file(jksPath)
            storePassword properties.getProperty("storePassword")
            v2SigningEnabled false
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportVersion"
    api "com.android.support:support-vector-drawable:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    api 'com.android.support:multidex:1.0.3'
    api 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    api 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    api 'org.greenrobot:eventbus:3.1.1'
    api 'com.github.bumptech.glide:glide:4.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    //工具类https://github.com/Blankj/AndroidUtilCode
    api 'com.blankj:utilcode:1.13.0'
}
